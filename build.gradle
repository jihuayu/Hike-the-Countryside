buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'


version = "${mod_version}-B${System.getenv().BUILD_NUMBER}.${System.getenv().BUILD_DATE}"
group = "roito.hikethecountryside"
archivesBaseName = "HikeTheCountryside-${minecraft_version}"

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "${minecraft_version}-${forge_version}"
    runDir = "run"
    
    mappings = "${mapping_version}"
	
	replace "@VERSION_INJECT@", project.version
    makeObfSourceJar = true
}

jar {
    manifest {
       attributes 'HCAT': 'hikethecountryside_at.cfg'
    }
}

task deobfJar(type: Jar) {
    from(sourceSets.main.output)
    archiveName = "${baseName}-${version}-deobf.${extension}"
}

artifacts {
    archives deobfJar
}

task apiJar(type: Jar) {
    classifier = 'api'
    from(sourceSets.main.output)
    from(sourceSets.main.allSource)
    include 'roito/hikethecountryside/api/**'
}

artifacts {
    archives apiJar
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

repositories {
    maven {
        // JEI
        name 'progwm\'s maven'
        url 'http://dvs1.progwml6.com/files/maven'
    }
    maven {
        // Hwyla && Guide-API
        url "http://tehnut.info/maven"
    }
    maven {
        // The One Probe
        url "https://maven.tterrag.com/"
    }

    flatDir {
        dirs 'libs'
    }
}

dependencies {
    deobfCompile "mezz.jei:jei_${minecraft_version}:${jei_version}"
    deobfCompile "mcp.mobius.waila:Hwyla:${hwyla_version}"
    deobfCompile("mcjty.theoneprobe:TheOneProbe-1.12:${theoneprobe_version}") {
        exclude group: 'cofh'
    }
    compile files(
        'libs/Kiwi-1.12.2-0.4.1.10-deobf.jar'
    )
}
